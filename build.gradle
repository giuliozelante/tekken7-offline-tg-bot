plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'io.micronaut.application' version '4.4.2' apply false
    id 'io.micronaut.aot' version '4.4.2' apply false
    id 'com.palantir.git-version' version '3.1.0'
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.aot'
    apply plugin: 'com.palantir.git-version'

    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor("org.projectlombok:lombok")
        compileOnly("org.projectlombok:lombok")
        runtimeOnly("ch.qos.logback:logback-classic")
        annotationProcessor("io.micronaut:micronaut-http-validation")
        annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
        implementation("io.micronaut.serde:micronaut-serde-jackson")
        implementation("io.micronaut:micronaut-http-client")
    }

    java {
        sourceCompatibility = JavaVersion.toVersion("21")
        targetCompatibility = JavaVersion.toVersion("21")
    }

    version = determineVersion()

    graalvmNative.toolchainDetection = false
    micronaut {
        processing {
            incremental(true)
            annotations("it.giuliozelante.tekken7.offline.tg.bot.*")
        }
        aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
            optimizeServiceLoading = false
            convertYamlToJava = false
            precomputeOperations = true
            cacheEnvironment = true
            optimizeClassLoading = true
            deduceEnvironment = true
            optimizeNetty = true
            replaceLogbackXml = true
        }
    }


    tasks.named("dockerfileNative") {
        jdkVersion = "21"
    }

}

def determineVersion() {
    def gitVersion = versionDetails()
    def lastTag = gitVersion.lastTag ?: '0.0'
    def (major, minor) = lastTag.tokenize('.') + [0, 0]
    def newMinor = (minor.toInteger() ?: 0) + 1
    return "${major}.${newMinor}"
}

